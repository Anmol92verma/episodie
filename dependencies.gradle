ext {
    android  = [
            compileSdkVersion:  25,
            buildToolsVersion: "25.0.3",
            minSdkVersion: 16,
            targetSdkVersion: 25,
            versionCode: 1,
            versionName: "1.0"
    ]

    build = [
            gradle : "3.0.0-alpha2",
            kotlin : "1.1.2-4"
    ]

    buildPlugins = [
            gradle : "com.android.tools.build:gradle:$build.gradle",
            kotlin : "org.jetbrains.kotlin:kotlin-gradle-plugin:$build.kotlin"
    ]

    // dependency versions
    def androidSupportLibs = '25.3.1'
    def versionJUnit = '4.12'
    def versionEspresso = '2.2.2'
    def versionRxJava = '1.2.0'

    def libs = [
            appCompat  : [group: 'com.android.support', name: 'appcompat-v7', version: androidSupportLibs],
            kotlin_stdlib : [group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib', version: build.kotlin],
            rxJava:          [group: 'io.reactivex', name: 'rxjava',    version: versionRxJava]
    ]

    def testLibs = [
            junit : [group: 'junit', name: 'junit', version: versionJUnit],
            espressoCore : [group: 'com.android.support.test.espresso', name: 'espresso-core', version: versionEspresso]
    ]

    def modules = [
            dataproviders: ':dataproviders',
            domain: ':domain',
            presentation: ':presentation'
    ]

    def supportAnnotationsExclude = {
        exclude group: 'com.android.support', module: 'support-annotations'
    }

    applicationModuleDependencies = [
            [configuration: "compile", dependency: libs.appCompat],
            [configuration: "compile", dependency: libs.kotlin_stdlib],
            [configuration: "testCompile", dependency: testLibs.junit],
            [configuration: "androidTestCompile", dependency: testLibs.espressoCore,
             options: supportAnnotationsExclude],

            [configuration: "compile", dependency: project(modules.domain)],
            [configuration: "compile", dependency: project(modules.presentation)]
    ]

    presentationModuleDependencies = [
            [configuration: "compile", dependency: libs.kotlin_stdlib],
            [configuration: "testCompile", dependency: testLibs.junit],
            [configuration: "compile", dependency: project(modules.domain)],
    ]

    domainModuleDependencies = [
            [configuration: "testCompile", dependency: testLibs.junit],
            [configuration: "compile", dependency: libs.kotlin_stdlib]
    ]

    dataprovidersModuleDependencies = [
            [configuration: "compile", dependency: libs.rxJava],
            [configuration: "compile", dependency: libs.kotlin_stdlib],
            [configuration: "testCompile", dependency: testLibs.junit],
            [configuration: "androidTestCompile", dependency: testLibs.espressoCore,
             options: supportAnnotationsExclude],

            [configuration: "compile", dependency: project(modules.domain)]
    ]

    def plugins = [
            kotlin_android: 'kotlin-android',
            kotlin: 'kotlin',
            apt: 'com.neenbedankt.android-apt',
            java: 'java'
    ]

    applicationModulePlugins = [ plugins.kotlin_android ]
    presentationModulePlugins = [ plugins.kotlin ]
    domainModulePlugins = [ plugins.kotlin ]
    dataprovidersModulePlugins = [ plugins.kotlin_android ]
}
